@echo off
chcp 65001
setlocal enabledelayedexpansion
mkdir video
for /r %~dp0 %%a in (entry.json) do (
  if exist %%a (
        :: 开始寻找entry.json
        :: 不能直接 !a!
        set dirJson=%%a
        echo entry.json = !dirJson!
        echo #1 end finding entry.json

        :: 获取 entry.json 的相对路径
        set dirJsonRelative=!dirJson:%cd%\=!
        :: 开始提取entry.json文件的上级和上上级目录名
        echo dirJsonRelative = !dirJsonRelative!
        for /f "delims=\ tokens=1,2" %%d in ("!dirJsonRelative!") do (
            set dirJsonRelativePath1=%%d
            set dirJsonRelativePath2=%%e
            echo dirJsonRelativePath1 = !dirJsonRelativePath1!
            echo dirJsonRelativePath2 = !dirJsonRelativePath2!
        )
        echo ##2 end getting short path
    
        :: 提取entry.json中的keyValue 对
        for /f "delims=, tokens=4" %%h in (!dirJson!) do (
            set keyValueTitle=%%h
            echo keyValueTitle = !keyValueTitle!
        )

        :: 提取keyValue对中的value
        for /f "delims=: tokens=2" %%k in ("!keyValueTitle!") do (
            set title=%%k
            set title=!title:"=!
            echo title = !title!        
            :: 剔除title中的特殊字符
            set aa=-
            rem 取消<的常用功能，进行转义处理
            set bb=^<
            set cc=^>
            set dd=/
            set ee=\
            call set "title=%%title:!aa!=%%"
            call set "title=%%title:!bb!=%%"
            call set "title=%%title:!cc!=%%"
            call set "title=%%title:!dd!=%%"
            call set "title=%%title:!ee!=%%"     
            echo title = !title!   
        ) 
        echo ###3 end getting title

        rem :: 进入entry.json的上一级
        rem :: 查找、提取*.blv的文件名，不含后缀
        cd %%~dpa
        for /r ./ %%n in (*.blv) do (    
            if exist %%n (
                echo *.blv = %%n
                set filenameSrcWithoutSuffix=%%~nn
                echo filenameSrcWithoutSuffix=!filenameSrcWithoutSuffix!
                set suffixDst=.flv
                set filenameDst=!dirJsonRelativePath1!_!dirJsonRelativePath2!_!title!_!filenameSrcWithoutSuffix!!suffixDst!
                echo filenameDst = !filenameDst!
                copy %%n %~dp0\video\!filenameDst!
            )
        ) 
        echo ####4 end setting title
  )
)
pause